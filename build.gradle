plugins {
    id 'java'
    id 'groovy'
}

repositories {
    jcenter()
}


sourceSets {
    main {
        java.srcDirs = []
        groovy.srcDirs += ['src/main/java']
    }

    test {
        java.srcDirs = []
        groovy.srcDirs = ['src/test-commit/groovy', 'src/test-commit/java']
        resources.srcDirs = ['src/test-commit/resources']
    }

    jmh {
        groovy {
            srcDirs = ['src/test-jmh/groovy']
            compileClasspath += test.compileClasspath
            runtimeClasspath += test.runtimeClasspath
        }
    }
}

dependencies {
    compile 'com.google.guava:guava:19.0'
    compile 'org.slf4j:slf4j-api:1.7.18'
    compile 'org.codehaus.groovy:groovy-all:2.4.6'
    compile 'com.javadocmd:simplelatlng:1.3.1'
    compile 'redis.clients:jedis:2.8.0'

    compile 'com.fasterxml.jackson.core:jackson-core:2.8.0.rc2'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.0.rc2'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.8.0.rc2'

    compile 'mysql:mysql-connector-java:6.0.3'

    compile 'ch.qos.logback:logback-core:1.1.3'
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'org.slf4j:slf4j-api:1.7.12'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'cglib:cglib-nodep:3.2.2'
    testCompile 'com.orange.redis-embedded:embedded-redis:0.6'


    jmhCompile 'org.openjdk.jmh:jmh-core:1.12'
    jmhCompile 'org.openjdk.jmh:jmh-generator-annprocess:1.12'
}

task benchDoubleCompare(type: JavaExec, group: 'jmh',
        description: 'bench performance for different double compare strategies') {
    classpath = sourceSets.jmh.runtimeClasspath
    main = 'org.xtraktor.RawPoint_CompareDoubleBenchmark'
}

task benchGroovyJava(type: JavaExec, group: 'jmh',
        description: 'bench performance for java/groovy/groovy static implementations') {
    classpath = sourceSets.jmh.runtimeClasspath
    main = 'org.xtraktor.RawPoint_CompareGroovyJava'
}

task benchPreprocessor(type: JavaExec, group: 'jmh',
        description: 'bench performance for data preprocessor sorted/reversed input') {
    classpath = sourceSets.jmh.runtimeClasspath
    main = 'org.xtraktor.preprocessing.SimpleDataPreprocessorBenchmark'
}